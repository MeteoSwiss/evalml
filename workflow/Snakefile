# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,

import time

from evalml.config import ConfigModel

# validate configuration and add defaults
update_config(config, ConfigModel.model_validate(dict(config)).model_dump(mode="json"))


# load rules
# -----------------------------------------------------
include: "rules/common.smk"
include: "rules/summary.smk"
include: "rules/data.smk"
include: "rules/inference.smk"
include: "rules/verif.smk"
include: "rules/report.smk"
include: "rules/plot.smk"


# about workflow
# -----------------------------------------------------

RUN_CMD = Path(".evalml_snakemake_cmd.txt").read_text().strip()
CONFIG_HASH = short_hash_config()
WHEN = datetime.now().strftime("%Y%m%d")
CONFIG_FILE = workflow.config_settings.configfiles[0]
CONFIG_LABEL = config["config_label"] or CONFIG_FILE.stem
EXPERIMENT_NAME = f"{WHEN}_{CONFIG_LABEL}_{CONFIG_HASH}"
CANDIDATES = collect_all_candidates()

DATA_DIR = OUT_ROOT / "data"
LOGS_DIR = OUT_ROOT / "logs"
RESULTS_DIR = OUT_ROOT / "results" / EXPERIMENT_NAME


# optional messages, log and error handling
# -----------------------------------------------------


def _c(text: str, code: str) -> str:
    return f"\033[{code}m{text}\033[0m"


def _hr(char="‚îÄ", n=72):
    print(_c(char * n, "90"))


def _fmt_seconds(s: float) -> str:
    s = int(round(s))
    h = s // 3600
    m = (s % 3600) // 60
    sec = s % 60
    if h:
        return f"{h}h {m:02d}m {sec:02d}s"
    if m:
        return f"{m}m {sec:02d}s"
    return f"{sec}s"


onstart:
    global _T0
    _T0 = time.time()

    when_iso = datetime.now().strftime("%Y-%m-%d %H:%M:%S")

    print()
    _hr()
    print(_c("üöÄ EvalML workflow started", "1"))
    print(_c(f"   Time:        {when_iso}", "90"))
    print(_c(f"   Snakemake:   {RUN_CMD}", "90"))
    print(_c(f"   Workdir:     {Path.cwd()}", "90"))
    print(_c(f"   Config:      {CONFIG_FILE.name}", "90"))
    print(_c(f"   Experiment:  {EXPERIMENT_NAME}", "90"))
    print(_c(f"   Candidates:  {", ".join(CANDIDATES)}", "90"))
    print(_c(f"   Data dir:    {DATA_DIR}", "90"))
    print(_c(f"   Logs dir:    {LOGS_DIR}", "90"))
    print(_c(f"   Results dir: {RESULTS_DIR}", "90"))
    _hr()
    print()


onsuccess:
    dt = _fmt_seconds(time.time() - (_T0 or time.time()))

    print()
    _hr()
    print(_c("‚úÖ Workflow finished successfully", "1"))
    print(_c(f"   Duration:  {dt}", "90"))
    print(_c(f"   Results:   {RESULTS_DIR}", "90"))
    _hr()
    print()


onerror:
    dt = _fmt_seconds(time.time() - (_T0 or time.time()))

    print()
    _hr()
    print(_c("‚ùå Workflow failed", "1"))
    print(_c(f"   Snakemake:   {RUN_CMD}", "90"))
    print(_c(f"   Duration:       {dt}", "90"))
    print(_c(f"   Results:        {RESULTS_DIR}", "90"))
    print(_c(f"   Rule logs:      {LOGS_DIR}", "90"))
    print(_c(f"   Snakemake logs: .snakemake/logs/", "90"))
    print(_c(f"   Slurm logs:     .snakemake/slurm_logs/", "90"))
    _hr()
    print()


# target rules
# -----------------------------------------------------


rule experiment_all:
    """Target rule for experiment workflow."""
    input:
        expand(
            rules.report_experiment_dashboard.output,
            experiment=EXPERIMENT_NAME,
        ),
        expand(
            rules.verif_metrics_plot.output,
            experiment=EXPERIMENT_NAME,
        ),


rule showcase_all:
    """Target rule for showcase workflow."""
    input:
        expand(
            rules.make_forecast_animation.output,
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=CANDIDATES,
            param=["T_2M", "SP_10M"],
            region=["globe", "europe", "switzerland"],
            showcase=EXPERIMENT_NAME,
        ),
        expand(
            rules.plot_meteogram.output,
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=CANDIDATES,
            param=["T_2M", "SP_10M"],
            sta=["GVE", "KLO", "LUG"],
            showcase=EXPERIMENT_NAME,
        ),


rule sandbox_all:
    """Target rule for creating a sandbox for inference."""
    input:
        expand(
            rules.create_inference_sandbox.output.sandbox,
            run_id=CANDIDATES,
        ),


rule run_inference_all:
    """Run inference for all reference times as defined in the configuration."""
    input:
        expand(
            OUT_ROOT / "data/runs/{run_id}/{init_time}/raw",
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=CANDIDATES,
        ),


rule verif_metrics_all:
    input:
        expand(
            rules.verif_metrics.output,
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=CANDIDATES,
        ),


rule verif_metrics_plot_all:
    """Run verification plot metrics for all studies."""
    input:
        expand(
            rules.verif_metrics_plot.output,
            experiment=EXPERIMENT_NAME,
        ),
