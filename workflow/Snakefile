# Main entrypoint of the workflow.
# Please follow the best practices:
# https://snakemake.readthedocs.io/en/stable/snakefiles/best_practices.html,

from evalml.config import ConfigModel

# validate configuration and add defaults
update_config(config, ConfigModel.model_validate(dict(config)).model_dump(mode="json"))


# load rules
# -----------------------------------------------------
include: "rules/common.smk"
include: "rules/data.smk"
include: "rules/inference.smk"
include: "rules/verif.smk"
include: "rules/report.smk"
include: "rules/plot.smk"
include: "rules/summary.smk"
include: "rules/verif_obs.smk"

# prefer one rule because snakemake complains about ambiguous rules (same output)
ruleorder: prepare_inference_forecaster > prepare_inference_interpolator

# optional messages, log and error handling
# -----------------------------------------------------
onstart:
    print("\n--- Analysis started ---\n")


onsuccess:
    print("\n--- Workflow finished! ---\n")


onerror:
    print("\n--- An error occurred! ---\n")


# target rules
# -----------------------------------------------------
EXPERIMENT_HASH = short_hash_config()
EXPERIMENT_DATE = datetime.now().strftime("%Y%m%d")
EXPERIMENT_LABEL = (
    config["experiment_label"] or workflow.config_settings.configfiles[0].stem
)
EXPERIMENT_DIRNAME = f"{EXPERIMENT_DATE}_{EXPERIMENT_LABEL}_{EXPERIMENT_HASH}"



rule experiment_all:
    """Target rule for experiment workflow."""
    input:
        OUT_ROOT / f"results/experiment/{EXPERIMENT_DIRNAME}/metrics/dashboard",
        OUT_ROOT / f"results/experiment/{EXPERIMENT_DIRNAME}/metrics/plots",
        expand(
            OUT_ROOT / "data/runs/{run_id}/summary.md",
            run_id=collect_all_candidates(),
        ),
        expand(
            OUT_ROOT / "data/runs/{run_id}/fdbk_files/verSYNOP_{init_time}.nc",
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES_MEC],
            run_id=collect_all_candidates(),
        ),



rule showcase_all:
    """Target rule for showcase workflow."""
    input:
        expand(
            OUT_ROOT
            / "showcases/{run_id}/{init_time}/{init_time}_{param}_{region}.gif",
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=collect_all_candidates(),
            param=["T_2M", "SP_10M"],
            region=["globe", "europe", "switzerland"],
        ),
        expand(
            OUT_ROOT / "showcases/{run_id}/{init_time}/{init_time}_{param}_{sta}.png",
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=collect_all_candidates(),
            param=["T_2M", "SP_10M"],
            sta=["GVE", "KLO", "LUG"],
        ),
        expand(
            OUT_ROOT / "data/runs/{run_id}/summary.md",
            run_id=collect_all_candidates(),
        ),


rule sandbox_all:
    """Target rule for creating a sandbox for inference."""
    input:
        expand(
            rules.create_inference_sandbox.output.sandbox,
            run_id=collect_all_candidates(),
        ),


rule run_inference_all:
    """Run inference for all reference times as defined in the configuration."""
    input:
        expand(
            OUT_ROOT / "data/runs/{run_id}/{init_time}/grib",
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=collect_all_candidates(),
        ),
    output:
        inference_ok=touch(OUT_ROOT / f"run_inference_all.{EXPERIMENT_HASH}.ok")



rule verif_metrics_all:
    input:
        expand(
            rules.verif_metrics.output,
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES],
            run_id=collect_all_candidates(),
        ),


rule verif_metrics_plot_all:
    """Run verification plot metrics for all studies."""
    input:
        expand(
            rules.verif_metrics_plot.output,
            experiment=EXPERIMENT_HASH,
        ),


rule verif_obs_all:
    input:
        expand(
            rules.run_mec.output,
            init_time=[t.strftime("%Y%m%d%H%M") for t in REFTIMES_MEC],
            run_id=collect_all_candidates(),
        ),

